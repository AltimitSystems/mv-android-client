/*
 * Copyright (c) 2017-2018 Altimit Community Contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or imp
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

/**
 * Extensions
 */
def EXTENSIONS = [
        [ module: 'libandroidapi', extension: 'systems.altimit.libandroidapi.AndroidAPI' ],
        //[ module: 'libgoogleplay', extension: 'systems.altimit.libgoogleplay.GooglePlay' ],
]

android {

    /**
     * Advanced Configuration
     */
    def BACK_BUTTON_QUITS   = true // Use back button to quit the app?
    def SHOW_FPS            = false // Display the FPS monitor?

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {

        /**
         * Project Identification
         */
        applicationId "id.application.rpgmakermv" // Change this to your applicationId.
        versionCode 1 // Set this to a higher number for game updates.
        versionName "1.0" // This is the version that the player sees.
    }

    buildTypes {
        buildTypes.each { buildType ->
            buildType.buildConfigField "boolean",  "BACK_BUTTON_QUITS", String.valueOf(BACK_BUTTON_QUITS)
            buildType.buildConfigField "boolean",  "SHOW_FPS",          String.valueOf(SHOW_FPS)
            buildType.buildConfigField "String[]", "EXTENSION_CLASSES", "{" + EXTENSIONS.collect {"\"${it.extension}\""}.join(",")  + "}"
        }
    }

    flavorDimensions "mv_android_client"

    productFlavors {
        webview {
            dimension "mv_android_client"
            minSdkVersion 14
        }

        // WARNING: Gecko engine is currently not functional
        zz_geckoalpha {
            dimension "mv_android_client"
            minSdkVersion 26
        }

        // WARNING: As of February 2017 Crosswalk is no longer maintained https://crosswalk-project.org/blog/crosswalk-final-release.html
        zz_crosswalk {
            dimension "mv_android_client"
            minSdkVersion 16
        }
    }
}

repositories {
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation project(':clientapi')

    zz_geckoalphaImplementation 'org.mozilla.geckoview:geckoview-beta-armeabi-v7a:+'

    zz_crosswalkImplementation 'org.xwalk:xwalk_core_library:23.53.589.4'

    // Include any Client API Extensions
    if (EXTENSIONS) {
        EXTENSIONS.each { extension ->
            implementation project(extension.module.startsWith(':') ? extension.module : ':' + extension.module)
        }
    }
}